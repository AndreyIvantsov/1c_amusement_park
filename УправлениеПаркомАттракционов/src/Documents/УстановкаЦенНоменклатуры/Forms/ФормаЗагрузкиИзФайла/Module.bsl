// @strict-types

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработатьФайлЦен(Параметры.АдресФайла);	
	ПодобратьНоменклатуру();
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	Адрес = ПоместитьТаблицуВоВременноеХранилище();
	Закрыть(Адрес);
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

// Обработать файла цен
// Параметры:
//  Адрес - Строка - Адрес файла во временном хранилище
//
&НаСервере
Процедура ОбработатьФайлЦен(Адрес)
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(Адрес);
	ИмяФайла = ПолучитьИмяВременногоФайла("xlsx");
	
	ДвоичныеДанные.Записать(ИмяФайла);
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.Прочитать(ИмяФайла);
	
	Для НомерСтроки = 2 По ТабличныйДокумент.ВысотаТаблицы Цикл
		Наименование = ТабличныйДокумент.Область(НомерСтроки, 1).Текст;
		Цена = ТабличныйДокумент.Область(НомерСтроки, 2).Текст;
		Строка = ДанныеФайла.Добавить();
		Строка.Наименование = Наименование;
		Строка.Цена = Число(Цена);
	КонецЦикла;
	
	УдалитьФайлы(ИмяФайла);

КонецПроцедуры

&НаСервере
Процедура ПодобратьНоменклатуру()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДанныеИзФайла.Наименование КАК Наименование,
	|	ДанныеИзФайла.Цена КАК Цена
	|ПОМЕСТИТЬ втДанныеФайла
	|ИЗ
	|	&ДанныеИзФайла КАК ДанныеИзФайла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанныеФайла.Наименование,
	|	втДанныеФайла.Цена,
	|	МАКСИМУМ(Номенклатура.Ссылка) КАК Номенклатура
	|ИЗ
	|	втДанныеФайла КАК втДанныеФайла
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Номенклатура
	|		ПО втДанныеФайла.Наименование = Номенклатура.Наименование
	|		И НЕ Номенклатура.ПометкаУдаления
	|СГРУППИРОВАТЬ ПО
	|	втДанныеФайла.Наименование,
	|	втДанныеФайла.Цена");
	
	Запрос.УстановитьПараметр("ДанныеИзФайла", ДанныеФайла.Выгрузить());
	ДанныеФайла.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТаблицуВоВременноеХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(ДанныеФайла.Выгрузить(, "Номенклатура, Цена"));
	
КонецФункции

#КонецОбласти
